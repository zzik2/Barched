plugins {
    id 'dev.architectury.loom' version '1.13-SNAPSHOT' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
}

architectury {
    minecraft = project.minecraft_version
}

allprojects {
    group = rootProject.maven_group
    version = rootProject.mod_version
}

subprojects {
    apply plugin: 'dev.architectury.loom'
    apply plugin: 'architectury-plugin'
    apply plugin: 'maven-publish'

    base {
        // Set up a suffixed format for the mod jar names, e.g. `example-fabric`.
        archivesName = "$rootProject.archives_name-$project.name"
    }

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
        mavenCentral()
        mavenLocal()
        maven { url "https://maven.shedaniel.me/" }
    }

    dependencies {
        minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
        mappings loom.officialMojangMappings()

        implementation("kr.zzik2:zreflex:$rootProject.zreflex_version")
    }

    java {
        // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
        // if it is present.
        // If you remove this line, sources will not be generated.
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    // Configure Maven publishing.
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = base.archivesName.get()
                from components.java
            }
        }

        // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
        repositories {
            // Add repositories to publish to here.
            // Notice: This block does NOT have the same function as the block in the top level.
            // The repositories here will be used for publishing your artifact, not for
            // retrieving dependencies.
        }
    }

    pluginManager.withPlugin('com.github.johnrengelman.shadow') {
        shadowJar {
            relocate 'net.minecraft.client.renderer.entity.CamelHuskRenderer', 'zzik2.barched.minecraft.client.renderer.entity.CamelHuskRenderer'
            relocate 'net.minecraft.client.renderer.entity.ParchedRenderer', 'zzik2.barched.minecraft.client.renderer.entity.ParchedRenderer'
            relocate 'net.minecraft.util.datafix.schemas.V4656', 'zzik2.barched.minecraft.util.datafix.schemas.V4656'
            relocate 'net.minecraft.world.entity.animal.CamelHusk', 'zzik2.barched.minecraft.world.entity.animal.CamelHusk'
            relocate 'net.minecraft.world.entity.monster.Parched', 'zzik2.barched.minecraft.world.entity.monster.Parched'
            relocate 'net.minecraft.world.item.SpearItem', 'zzik2.barched.minecraft.world.item.SpearItem'
        }
    }
}
